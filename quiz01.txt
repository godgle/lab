'filename: quiz01.kas
'//연결형문제
SCREEN ,,,,500,350,"퀴즈데모",1,,"ai.ico"
DSHAPE "back019.bmp",0,0,GXY(3)+1,GXY(4)+1
SHAPELOAD 0,"c001.bmp",100,5,2,0,0,100,90,0,0,13 '정답그림
SHAPELOAD 1,"c003.bmp",100,5,2,0,0,100,90,0,0,13 '오답그림
ANIMATE 0,1,1,1,1,0,1,0,1,48,44,60,60,90,30,0
PRINT 146,25,534,"ⓩⓔ7,ⓨ8ⓥ18,다음은 곤충의 이름과 그림들이다ⓝ서로 맞는 것끼리 줄로 이으시오."
BOX 119,12,457,83,131,,15,2,0,0
TEXT 67,108,"매미",2,20,,,9,,,,7
TEXT 186,108,"전갈",2,20,,,9,,,,7
TEXT 307,108,"메뚜기",2,20,,,9,,,,7
TEXT 423,108,"달팽이",2,20,,,9,,,,7
DSHAPE "달팽이.bmp",34,198,100,82,1
DSHAPE "매미.bmp",153,194,69,82,1
DSHAPE "전갈.bmp",249,197,100,82,1
DSHAPE "메뚜기.bmp",355,198,116,82,1
CALL 연결형문제,*TF,4,1,"02030401",59,132,18,17,101,32 '답처리
IF TF=1 '정답판정
  SOUND "yes.wav",100,,0
  TEXT 51,93,"맞았습니다",2,12,1,,9,,,,7
  ANIMATE 0,1,10,1,1,0,1,100,1,48,44
ELSE
  SOUND "no.wav",100,,0
  TEXT 51,93,"틀렸습니다",2,12,1,,9,,,,7
  ANIMATE 1,1,10,1,1,0,1,150,1,48,44
ENDIF
ANIMATE
PAUSE

SUB 연결형문제,*정답판정,개수,배열,정답$,X,Y,가로크기,세로크기,가로간격,세로간격
  '<<<select로 화면 편집한 결과(X위치~끝)를 5번째 인수부터 복사하여 사용>>>  
  '(종료조건) : 보기가 모두 연결시
  ' *정답판정         :정오답 판정 결과(0:오답, 1:정답)
  ' 개수              :나열될 보기 개수(최대80)
  ' 배열              :보기배열(0:세로, 1:가로)
  ' 정답$             :정답개수만큼 두자리형("00")으로 나열
  ' X,Y               :출력위치
  ' 가로크기,세로크기 :버튼의 크기(15*15)
  ' 가로간격,세로간격 :항목간의 간격
  LOCAL D$,C,I,N,X,Y,F=0,S=0,개수,배열,간격X,간격Y,여백X,여백Y,정답[80],F[80],S[80]
  간격X=가로크기+가로간격-15
  간격Y=세로크기+세로간격-15
  IF 배열=0'세로배열
    FOR I,0,개수-1
      BUTTONDRAW X,Y+I*(15+간격Y),15,15,2,0
      BUTTONDRAW X+15+간격X,Y+I*(15+간격Y),15,15,2,0
    NEXT
  ELSE '가로배열
    FOR I,0,개수-1
      BUTTONDRAW X+I*(15+간격X),Y,15,15,2,0
      BUTTONDRAW X+I*(15+간격X),Y+15+간격Y,15,15,2,0
    NEXT
  ENDIF

  정답$=string(정답$+string("  ",개수),,1,개수*2)
  FOR I,1,개수
    정답[I]=VAL(STRING(정답$,,I*2-1,I*2))
    F[I]=0
    S[I]=0
  NEXT
  정답판정=0

  IF 배열=0'세로배열
    WHILE 1
      SELECT 답,3,,,,,개수*2,2,개수,X,Y,15,15,간격X,간격Y
      IF (답%2)=1'1차배열일경우
        N=INT(답/2)+1
        IF F<>0'1차배열위치이동(이전위치지움)
          BUTTONDRAW X,Y+(F-1)*(15+간격Y),15,15,2,0
          F[F]=0
          F=0
        ENDIF
        IF F[N]=0'2차배열이연결안되었을때
          BUTTONDRAW X,Y+(N-1)*(15+간격Y),15,15,2,2
          F[N]=-1
          F=N
          S=0
        ELSE
          BUTTONDRAW X,Y+(N-1)*(15+간격Y),15,15,2,0
          IF F[N]>0'1,2차연결된것누를때
            BUTTONDRAW X+15+간격X,Y+(F[N]-1)*(15+간격Y),15,15,2,0
            LINE X+15,Y+(N-1)*(15+간격Y)+7,X+15+간격X,Y+(F[N]-1)*(15+간격Y)+7,15,,,,1
            S[F[N]]=0
            S=0
          ENDIF
          F[N]=0
          F=0
        ENDIF
      ELSE '2차배열일경우
        IF F<>0'1차배열이지정되었을때
          N=INT(답/2)
          IF S[N]=0'1차배열이연결안되었을때
            IF S<>0'2차배열위치이동(이전위치지움)
              BUTTONDRAW X+15+간격X,Y+(S-1)*(15+간격Y),15,15,2,0
              LINE X+15,Y+(F-1)*(15+간격Y)+7,X+15+간격X,Y+(S-1)*(15+간격Y)+7,15,,,,1
              S[S]=0
              S=0
            ENDIF
            S[N]=F
            S=N
            BUTTONDRAW X+15+간격X,Y+(S-1)*(15+간격Y),15,15,2,2
            LINE X+15,Y+(F-1)*(15+간격Y)+7,X+15+간격X,Y+(S-1)*(15+간격Y)+7,15,,,,1
            F[F]=N
            F=0
          ELSE
          ENDIF
        ENDIF
      ENDIF
      '정답판정
      정답판정=1
      FOR I,1,개수
        IF F[I]<>정답[I]
          정답판정=0
          EXIT
        ENDIF
      NEXT
      C=0
      FOR I,1,개수
        C=C+(S[I]>0)
      NEXT
      IF C=>개수
        EXIT
      ENDIF
    WEND
  ELSE '가로배열
    WHILE 1
      SELECT 답,3,,,,,개수*2,개수,2,X,Y,15,15,간격X,간격Y
      IF 답<=개수'1차배열일경우
        N=답
        IF F<>0'1차배열위치이동(이전위치지움)
          BUTTONDRAW X+(F-1)*(15+간격X),Y,15,15,2,0
          F[F]=0
          F=0
        ENDIF
        IF F[N]=0'2차배열이연결안되었을때
          BUTTONDRAW X+(N-1)*(15+간격X),Y,15,15,2,2
          F[N]=-1
          F=N
          S=0
        ELSE
          BUTTONDRAW X+(N-1)*(15+간격X),Y,15,15,2,0
          IF F[N]>0'1,2차연결된것누를때
            BUTTONDRAW X+(F[N]-1)*(15+간격X),Y+15+간격Y,15,15,2,0
            LINE X+(N-1)*(15+간격X)+7,Y+15,X+(F[N]-1)*(15+간격X)+7,Y+15+간격Y,15,,,,1
            S[F[N]]=0
            S=0
          ENDIF
          F[N]=0
          F=0
        ENDIF
      ELSE '2차배열일경우
        IF F<>0'1차배열이지정되었을때
          N=답-개수
          IF S[N]=0'1차배열이연결안되었을때
            IF S<>0'2차배열위치이동(이전위치지움)
              BUTTONDRAW X+(S-1)*(15+간격X),Y+15+간격Y,15,15,2,0
              LINE X+(F-1)*(15+간격X)+7,Y+15,X+(S-1)*(15+간격X)+7,Y+15+간격Y,15,,,,1
              S[S]=0
              S=0
            ENDIF
            S[N]=F
            S=N
            BUTTONDRAW X+(S-1)*(15+간격X),Y+15+간격Y,15,15,2,2
            LINE X+(F-1)*(15+간격X)+7,Y+15,X+(S-1)*(15+간격X)+7,Y+15+간격Y,15,,,,1
            F[F]=N
            F=0
          ELSE
          ENDIF
        ENDIF
      ENDIF
      '정답판정
      정답판정=1
      FOR I,1,개수
        IF F[I]<>정답[I]
          정답판정=0
          EXIT
        ENDIF
      NEXT
      C=0
      FOR I,1,개수
        C=C+(S[I]>0)
      NEXT
      IF C=>개수
        EXIT
      ENDIF
    WEND
  ENDIF
ENDSUB
