'filename: sokoban.kas

SCREEN 0,1,,,596,436,,4,,,RGB(255,255,0),"main.bmp"
VARMEM 1024*10

DIM _null2[21,21]  _ok[20,2]  

FONTSET "Times New Roman"
SHAPELOAD 0,"so03.gif",1,1,1,,,24,24,,,RGB(192,192,192) 
SHAPELOAD 1,"so02.gif",4,4,1,,,24,24,,,RGB(192,192,192) 
SHAPELOAD 2,"so01.gif",18,18,1,,,24,24,,,RGB(192,192,192)
SHAPELOAD 4,"button.bmp",15,5,3,,,29,29
SHAPELOAD 5,"level.bmp",30,3,10,,,87,26
SHAPELOAD 10,"so05.gif",1,1,1,,,35,37

score=1000

menu:
VIDEOCLOSE
TIMEROFF
ANIMATE
CALL btn
TIMEROFF 1
DSHAPE "main.bmp",0,,,,2,,RGB(255,255,0)
FOR i,1,5
  BUTTONSET i,,,,41+108*(i-1),225,87,26,5,3*(i-1)+1,3*(i-1)+2,3*(i-1)+3,2,4,,game,0
NEXT
FOR i,6,10
  BUTTONSET i,,,,41+108*(i-6),372,87,26,5,3*(i-1)+1,3*(i-1)+2,3*(i-1)+3,2,4,,game,0
NEXT

BUTTONSET 105,,,,545,34,29,29,4,5,10,15,2,4,,end,1    'end
WHILE 1
WEND

game:
level=buttoninfo()
FOR i,1,10
  BUTTONOFF i,1,0
NEXT

'=============background======================
SHAPE
DSHAPE "base.bmp",0,0,,,1,,RGB(255,255,0)
GET 1,212,71,419,91'timer
GET 2,315,37,374,59'score
GET 3,11,223,47,247'move
GET 4,11,279,47,303'push
GET 5,79,105,576,411'back
GET 6,11,170,47,194'level

'=============background======================

restart:
VIDEOCLOSE
ANIMATE
TIMEROFF
TIMERSET 1,1000,timer
TIMERSET 2, 500,time2
move=0, push=0, save=0, game=0, balls=0, oxcount=0, time=0, min=0

FOR i,1,10 
  _ok[i,1]=0
  _ok[i,2]=0
NEXT

level$=format{"%02d",level},10   'two level
RESTORE label{"so"+level$}    'goto label
READ xend, yend, _x0, _y0    

anix=_x0+12,aniy=_y0+12       'animate 
push1=0,move1=0

PUT 1
PUT 3
PUT 4
PUT 6
PUT 5

FOR i,0,xend
  FOR j,0,yend
    _null2[i,j]=0  'init
  NEXT
NEXT

FOR j,1,yend
  FOR i,1,xend
    READ imsi
    CALL Draw,i,j
  NEXT
NEXT

TEXT 370,41,score,1,16,0,,9,0,0,0,0  'score
TEXT 30,174,level,2,18,0,,7,,,,7    'level
TEXT 30,228,move,2,18,0,,7,,,,7     'move num
TEXT 30,283,push,2,18,0,,7,,,,7     'push num

TIMERON 1

BUTTONSET 101,,,,409,34,29,29,4,1,6,11,2,4,,restart,1 'reload
IF level<>1
  BUTTONSET 102,,,,443,34,29,29,4,3,8,13,2,4,,restart,1 'prev
ENDIF
BUTTONSET 103,,,,477,34,29,29,4,2,7,12,2,4,,menu,0 'up
IF level<>10
  BUTTONSET 104,,,,511,34,29,29,4,4,9,14,2,4,,restart,1 'next
ENDIF
BUTTONSET 105,,,,545,34,29,29,4,5,10,15,2,4,,end,1    'end
MOUSECURSOR 0

'/////////////▼▼execute routine▼▼//////////////////////////
Keychk:
IF move1=1
  PUT 3
  TEXT 30,228,move,2,18,0,,7,,,,7     'move num
  move1=0
ENDIF
IF push1=1
  PUT 4
  TEXT 30,283,push,2,18,0,,7,,,,7'push num
  push1=0
ENDIF

CALL Check,1   'all state check
IF game=1
  game =0
  GOTO stop
ENDIF
WHILE kbchk()<>0   'key check
  b=kbchk()
WEND

'///// ▼▼ key check  :  293(left),294(Up),295(right),296(down) ▼▼/////
READKEY key   
rkey=(key=293)*1+(key=294)*2+(key=295)*3+(key=296)*4
ONGOTO rkey,Left,Up,Right,Down
GOTO Keychk

Left:
IF (_xst<>0)
  IF (_null2[_xst-1,_yst]>1)&(_xst<>1)
    IF (_null2[_xst-2,_yst]=0) 'push
      FOR i,1,24
        ANIMATE 1,1,1,1,1,0,1,0,1,anix+(_xst-1)*24-i,aniy+_yst*24,,,,,_null2[_xst-1,_yst] 
        ANIMATE 0,1,1,1,1,,1,0,1,anix+_xst*24-i,aniy+_yst*24,,,,,15
      NEXT

      _null2[_xst-2,_yst]=_null2[_xst-1,_yst]
      _null2[_xst-1,_yst]=0
      push=push+1
      push1=1
      _xst=_xst-1
      GOTO Keychk
    ENDIF
  ENDIF
  IF (_null2[_xst-1,_yst]=0)
    ANIMATE 0,1,1,1,1,,3,1,2,anix+_xst*24,aniy+_yst*24,anix+(_xst-1)*24,aniy+_yst*24,,,15
    _xst=_xst-1
    move=move+1
    move1=1
  ENDIF
ENDIF
GOTO Keychk

Up:
IF (_yst<>0)
  IF (_null2[_xst,_yst-1]>1)&(_yst<>1)
    IF (_null2[_xst,_yst-2]=0) 'push
      FOR i,1,24
        ANIMATE 1,1,1,1,1,,1,0,1,anix+_xst*24,aniy+(_yst-1)*24-i,,,,,_null2[_xst,_yst-1]
        ANIMATE 0,1,1,1,1,,1,0,1,anix+_xst*24,aniy+_yst*24-i,,,,,15   'move
      NEXT
      _null2[_xst,_yst-2]=_null2[_xst,_yst-1]
      _null2[_xst,_yst-1]=0
      push=push+1
      push1=1
      _yst=_yst-1
      GOTO Keychk
    ENDIF
  ENDIF
  IF (_null2[_xst,_yst-1]=0)
    ANIMATE 0,1,1,1,1,,3,1,2,anix+_xst*24,aniy+_yst*24,anix+_xst*24,aniy+(_yst-1)*24,,,15  'move
    _yst=_yst-1
    move=move+1
    move1=1
  ENDIF
ENDIF
GOTO Keychk

Right:
IF (_null2[_xst+1,_yst]>1)&(_xst<>xend)
  IF (_null2[_xst+2,_yst]=0) 'push
    FOR i,1,24
      ANIMATE 1,1,1,1,1,0,1,0,1,anix+(_xst+1)*24+i,aniy+_yst*24,,,,,_null2[_xst+1,_yst]
      ANIMATE 0,1,1,1,1,,1,0,1,anix+_xst*24+i,aniy+_yst*24,,,,,15   'move
    NEXT
    _null2[_xst+2,_yst]=_null2[_xst+1,_yst]
    _null2[_xst+1,_yst]=0
    push=push+1
    push1=1
    _xst=_xst+1
    GOTO Keychk
  ENDIF
ENDIF
IF (_xst<>xend) 'move
  IF (_null2[_xst+1,_yst]=0)
    ANIMATE 0,1,1,1,1,,2,1,2,anix+_xst*24,aniy+_yst*24,anix+(_xst+1)*24,aniy+_yst*24,,,15
    _xst=_xst+1
    move=move+1
    move1=1
  ENDIF
ENDIF
GOTO Keychk

Down: 
IF (_null2[_xst,_yst+1]>1)&(_yst<>yend)
  IF (_null2[_xst,_yst+2]=0) 'push
    FOR i,1,24
      ANIMATE 1,1,1,1,1,0,1,0,1,anix+_xst*24,aniy+(_yst+1)*24+i,,,,,_null2[_xst,_yst+1]
      ANIMATE 0,1,1,1,1,,1,0,1,anix+_xst*24,aniy+_yst*24+i,,,,,15 'move
    NEXT
    _null2[_xst,_yst+2]=_null2[_xst,_yst+1]
    _null2[_xst,_yst+1]=0
    push=push+1
    push1=1
    _yst=_yst+1
    GOTO Keychk
  ENDIF
ENDIF

IF (_yst<>yend) 'move
  IF (_null2[_xst,_yst+1]=0)
    ANIMATE 0,1,1,1,1,,3,1,2,anix+_xst*24,aniy+_yst*24,anix+_xst*24,aniy+(_yst+1)*24,,,15 'move
    _yst=_yst+1
    move=move+1
    move1=1
  ENDIF
ENDIF
GOTO Keychk

stop:
WHILE 1
WEND


'%%%%%%%%%%%%%%%%% sub routine %%%%%%%%%%%%%%%%%%%%%%%%%

SUB timer
  tx=17*time
  IF time=0
    PUT 1
  ENDIF
  BOX 217+tx,76,217+12+tx,76+11,0,0,12,1
  time=time+1
  IF time=12
    score=score-10
    PUT 2
    TEXT 370,41,score,1,16,0,,9,0,0,0,0'score
    time=0
  ENDIF
ENDSUB

SUB Draw,ii,jj
  IF (imsi>0)&(imsi<20)  
    SHAPE 2,imsi,_x0+(ii-1)*24,_y0+(jj-1)*24,,,1
    _null2[ii-1,jj-1]=1
  ENDIF

  IF imsi=21 'star
    balls=balls+1
    ANIMATE 1,1,1,1,1,,1,,1,anix+(ii-1)*24,aniy+(jj-1)*24,,,,,balls+1
    _null2[ii-1,jj-1]=balls+1  'object
  ENDIF

  IF imsi=22 'eobject
    oxcount=oxcount+1
    _ok[oxcount,1]=ii-1
    _ok[oxcount,2]=jj-1
    SHAPE 1,2,_x0+(ii-1)*24,_y0+(jj-1)*24,,,1 'wall
  ENDIF
  IF imsi=23
    _xst=ii-1
    _yst=jj-1
    ANIMATE 0,1,1,1,1,,1,,1,anix+(_xst)*24,aniy+(_yst)*24,,,,,15   
  ENDIF
ENDSUB


SUB Check
  save=0
  FOR i,1,balls
    IF _null2[_ok[i,1],_ok[i,2]]>1
      save=save+1
      ANIMATE 1,3,3,1,1,0,1,0,1,anix+_ok[i,1]*24,aniy+_ok[i,2]*24,,,,,_null2[_ok[i,1],_ok[i,2]]  'color
    ENDIF
  NEXT
  IF save=balls
    CALL success,1
  ENDIF
ENDSUB

SUB success    'if success
  BUTTONOFF ,0,0
  TIMEROFF 1

  game=1
  ANIMATE
  WINDOW 168,144,468,369,0
  BOX 174,151,460,362,0,0,RGB(247,243,213),3
  VIDEOOPEN 1,"so05.gif",290,164,35,37,,0
  VIDEO 1,3
  SHAPE 1,3,184,160,24,24,1
  SHAPE 1,3,426,159,24,24,1
  SHAPE 1,3,183,329,24,24,1
  SHAPE 1,3,425,329,24,24,1
  s3=1
  TIMERON 2
  bonus=1000
  TEXT 282,214,"bonus : ",2,18,0,,9,0,0,0,0

  TEXT 348,215,"2000",1,16,0,,9,0,0,0,0

  TEXT 248,252,"moves :",2,18,0,,9,0,0,0,0
  TEXT 292,252,move,2,16,0,,9,0,0,0,0
  TEXT 314,253,"×(-10)  =",,18,0,,9,0,0,0,0
  TEXT 390,252,move*(-10),0,16,0,,9,0,0,0,0

  TEXT 248,284,"pushes :",2,18,0,,9,0,0,0,0
  TEXT 294,284,push,2,16,0,,9,0,0,0,0
  TEXT 319,284,"× (-5)  =",,18,0,,9,0,0,0,0
  TEXT 390,284,push*(-5),,16,0,,9,0,0,0,0

  BOX 306,331,366,352,0,0,52,1,0,0
  TEXT 269,335,"score :",2,18,0,,9,0,0,0,0
  TEXT 358,335,score,1,16,0,,9,0,0,0,0

  score1=2000-move*10-push*5  'score sum
  IF score1<100  'low score 100
    score1=100
  ENDIF
  div=score1/100
  mod=(score1-100*div)/5
  WAIT 500
  FOR i,1,div
    BOX 306,331,366,352,0,0,52,1,0,0
    TEXT 358,335,score+i*100,1,16,0,,9,0,0,0,0
    WAIT 50
    BEEP 10,1
  NEXT
  score=score+div*100

  FOR i,1,mod
    BOX 306,331,366,352,0,0,52,1,0,0
    TEXT 358,335,score+i*5,1,16,0,,9,0,0,0,0
    WAIT 50
    BEEP 10,1
  NEXT
  score=score+mod*5
  PUT 2
  TEXT 370,41,score,1,16,0,,9,0,0,0,0  'score
  BUTTONON -1
  PAUSE 2
  VIDEOCLOSE 1
  TIMEROFF 2
  EWINDOW
ENDSUB

SUB time2
  SHAPE 1,(s3%2+3),184,160,24,24
  SHAPE 1,(s3%2+3),426,159,24,24
  SHAPE 1,(s3%2+3),183,329,24,24
  SHAPE 1,(s3%2+3),425,329,24,24
  s3=s3+1
ENDSUB


SUB restart
  IF buttoninfo()=102
    level=level-1
  ELSE
    IF buttoninfo()=104
      level=level+1
    ENDIF
  ENDIF
  ANIMATE
  CALL btn
  EXIT sub,restart
ENDSUB

SUB btn
  FOR i,101,105
    BUTTONOFF i,1,0
  NEXT
ENDSUB

SUB end
  IF INSTR(URLINFO(5),"/")
    HTMLSTART
      HTML <html>
      HTML <head>
      HTML </head>
      HTML <body onload=window.close()>
      HTML </BODY>
      HTML </html>
    HTMLEND
  ELSE
    END
  ENDIF
  END
ENDSUB


so01:
DATA 7,5,231,195
DATA 1,2,2,2,2,2,3
DATA 4,0,0,0,0,22,4
DATA 4,0,0,21,21,22,4
DATA 4,0,0,7,23,0,4
DATA 6,2,2,12,2,2,5

so02:
DATA 7,7,231,171
DATA 1,2,2,2,2,2,3
DATA 4,0,0,0,0,0,4
DATA 4,0,0,21,21,0,4
DATA 4,0,21,0,21,23,4
DATA 4,22,22,1,2,2,5
DATA 4,22,22,4,0,0,0
DATA 6,2,2,5,0,0,0

so03:
DATA 19,11,100,123
DATA 0,0,0,0,1,2,2,2,3,0,0,0,0,0,0,0,0,0,0
DATA 0,0,0,0,4,0,0,0,4,0,0,0,0,0,0,0,0,0,0
DATA 0,0,0,0,4,21,0,0,4,0,0,0,0,0,0,0,0,0,0
DATA 0,0,1,2,5,0,0,21,6,3,0,0,0,0,0,0,0,0,0
DATA 0,0,4,0,0,21,0,21,0,4,0,0,0,0,0,0,0,0,0
DATA 1,2,5,0,7,0,1,3,0,4,0,0,0,1,2,2,2,2,3
DATA 4,0,0,0,8,0,6,5,0,6,2,2,2,5,0,0,22,22,4
DATA 4,0,21,0,0,21,23,0,0,0,0,0,0,0,0,0,22,22,4
DATA 6,2,2,2,3,0,9,2,10,0,7,0,1,3,0,0,22,22,4
DATA 0,0,0,0,4,0,0,0,0,0,11,2,12,12,2,2,2,2,5
DATA 0,0,0,0,6,2,2,2,2,2,5,0,0,0,0,0,0,0,0

so04:
DATA 8,5,219,195
DATA 0,1,2,2,2,2,2,3
DATA 1,5,0,0,0,0,22,4
DATA 4,0,0,0,21,21,23,4
DATA 4,0,0,0,7,0,22,4
DATA 6,2,2,2,12,2,2,5

so05:
DATA 7,7,236,171
DATA 0,1,2,2,2,2,3
DATA 1,5,0,0,0,22,4
DATA 4,22,0,0,21,0,4
DATA 4,21,15,21,15,0,4
DATA 4,23,0,0,21,0,4
DATA 4,22,0,0,7,22,4
DATA 6,2,2,2,12,2,5

so06:
DATA 7,6,231,183
DATA 1,2,2,2,3,0,0
DATA 4,0,0,0,6,2,3
DATA 4,22,22,21,21,22,4
DATA 4,0,21,21,23,0,4
DATA 4,0,0,0,22,1,5
DATA 6,2,2,2,2,5,0

so07:
DATA 7,7,231,171
DATA 1,2,2,2,2,2,3
DATA 4,22,0,22,22,22,4
DATA 4,0,21,0,15,0,4
DATA 4,21,15,21,21,23,4
DATA 4,0,0,0,0,1,5
DATA 4,0,0,0,1,5,0
DATA 6,2,2,2,5,0,0

so08:
DATA 9,9,207,147
DATA 1,2,2,2,2,2,2,3,0
DATA 4,0,0,0,0,0,0,4,0
DATA 4,0,15,21,21,0,0,4,0
DATA 4,0,22,22,22,15,0,4,0
DATA 6,3,22,22,22,21,0,6,3
DATA 0,4,0,9,10,0,21,0,4
DATA 0,4,21,0,0,21,0,0,4
DATA 0,4,0,0,7,0,0,23,4
DATA 0,6,2,2,12,2,2,2,5

so09:
DATA 13,8,159,159
DATA 0,0,0,0,0,0,0,0,1,2,2,2,3
DATA 1,2,2,2,2,2,2,2,5,0,0,0,4
DATA 4,0,0,22,22,22,22,22,22,21,0,0,4
DATA 4,0,0,0,15,21,9,2,3,0,0,0,4
DATA 6,2,3,0,21,0,21,23,4,0,0,0,4
DATA 0,0,4,0,21,0,21,0,4,0,0,0,4
DATA 0,0,4,0,0,0,0,0,11,2,2,2,5
DATA 0,0,6,2,2,2,2,2,5,0,0,0,0

so10:
DATA 11,10,183,135
DATA 0,0,0,1,13,2,2,2,3,0,0
DATA 1,2,2,12,5,0,0,0,4,0,0
DATA 4,0,21,0,0,22,21,0,4,0,0
DATA 4,0,0,0,22,22,1,13,14,0,0
DATA 4,0,21,0,21,21,6,12,14,0,0
DATA 6,13,3,0,22,21,22,0,6,2,3
DATA 0,11,5,21,22,22,22,0,21,0,4
DATA 0,4,0,21,22,1,3,0,0,23,4
DATA 0,4,0,0,0,11,12,2,2,2,5
DATA 0,6,2,2,2,5,0,0,0,0,0
